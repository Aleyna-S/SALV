*&---------------------------------------------------------------------*
*& Report Z_DENEME_ALV
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT Z_DENEME_ALV.

DATA: gt_sbook TYPE TABLE OF sbook,
      go_salv TYPE REF TO cl_salv_table.

START-OF-SELECTION.

SELECT * UP TO 20 ROWS FROM sbook
  INTO TABLE gt_sbook.

cl_salv_table=>factory(
  IMPORTING
    r_salv_table   = go_salv
  CHANGING
    t_table        = gt_sbook
).

*salv'nin görünümünü düzenlemek için oluşturuldu.
DATA: lo_display TYPE REF TO cl_salv_display_settings.
lo_display = go_salv->get_display_settings( ).
*başlık ekleniyor.
lo_display->set_list_header( value = 'SALV' ).
*satırlarda zebra görünüm elde ediliyor.
lo_display->set_striped_pattern( value = 'X' ).


*salv'nin sütunlarını düzenlemek için oluşturuldu. Bu düzenlemeler tüm sütunları kapsar.
DATA: lo_cols TYPE REF TO cl_salv_columns.
lo_cols = go_salv->get_columns( ).
*değerin uzunluğuna göre sütun genişliği ayarlanır.
lo_cols->set_optimize( value = 'X' ).


*tek bir sütunu düzenlemek için oluşturuldu.
DATA: lo_col TYPE REF TO cl_salv_column.
*öngörülemeyen hatalar için dump'a düşmemek adına hataları try-catch ile kontrol altına alırız.
TRY.
*hangi sütunda düzenleme yapacağımızı belirtir.
lo_col = lo_cols->get_column( columnname = 'INVOICE' ).
*sütunun uzunluğuna göre uzun, orta, kısa halinde hangi isimlendirilmede gözükeceğini düzeniyoruz.
lo_col->set_long_text( value = 'Yeni Fatura Düzenleyici' ).
lo_col->set_medium_text( value = 'Yeni Fatura D.' ).
lo_col->set_short_text( value = 'Yeni Fat' ).
CATCH cx_salv_not_found.
  ENDTRY.

TRY.
lo_col = lo_cols->get_column( columnname = 'MANDT' ).
*ilgili sütunu gizler
lo_col->set_visible( value = if_salv_c_bool_sap=>false ).
CATCH cx_salv_not_found.
  ENDTRY.


*toolbar butonları ekleme.
DATA: lo_func TYPE REF TO cl_salv_functions.
lo_func = go_salv->get_functions( ).
lo_func->set_all( abap_true ).


*header oluşturma.
DATA: lo_header TYPE REF TO cl_salv_form_layout_grid,
      lo_h_label TYPE REF TO cl_salv_form_label,
      lo_h_flow TYPE REF TO cl_salv_form_layout_flow.

CREATE OBJECT lo_header.

lo_h_label = lo_header->create_label( row = 1    column = 1 ).
lo_h_label->set_text( value = 'Header Oluşturuldu' ).
lo_h_flow = lo_header->create_flow( row = 2    column = 1 ).
lo_h_flow->create_text( EXPORTING text = 'Header2 Oluşturuldu' ).
go_salv->set_top_of_list( value = lo_header  ).


*popup ekranı oluşturma
go_salv->set_screen_popup(
  EXPORTING
    start_column = 10
    end_column   = 75
    start_line   = 5
    end_line     = 25
).

go_salv->display( ).
